/*
   Simple +/-/* expression language; 
   parser evaluates constant expressions on the fly
*/

package cyr7.parser;

import java_cup.runtime.*;
import java_cup.runtime.ComplexSymbolFactory.ComplexSymbol;
import cyr7.ast.*;
import cyr7.ast.expr.*;
import cyr7.ast.expr.access.*;
import cyr7.ast.expr.binexpr.*;
import cyr7.ast.expr.binexpr.BinExprNode.ASTOpType;
import cyr7.ast.expr.literalexpr.*;
import cyr7.ast.expr.unaryexpr.*;
import cyr7.ast.toplevel.*;
import cyr7.ast.stmt.*;
import cyr7.ast.stmt.CompoundAssignStmtNode.CompoundOpType;
import cyr7.ast.type.*;
import cyr7.exceptions.parser.*;
import cyr7.lexer.LexerUtil;
import cyr7.lexer.MyLexer;
import java.util.List;
import java.util.LinkedList;
import java.util.Collections;
import java.util.Optional;

parser code {:

@Override
public void unrecovered_syntax_error(Symbol token) throws Exception {
    ComplexSymbol cur_token = (ComplexSymbol) token;
    if (cur_token.sym == sym.INT_MAX) {
        throw new ParserIntegerOverflowException(cur_token.xleft);
    } else {
        throw new UnexpectedTokenException(cur_token);
    }
}

@Override
public void syntax_error(Symbol cur_token) {
    // Do nothing
}

:};

action code {:
:}

/* define how to connect to the scanner! */
init with {:
:};

scan with {: 
    Symbol sym = getScanner().next_token();
    return (sym != null) ? sym : 
                       getSymbolFactory().newSymbol("END_OF_FILE",EOF_sym());
:};

/* Terminals (tokens returned by the scanner). */


terminal IXI_FILE, XI_FILE;
// Punctuation
terminal IF, ELSE, RETURN, USE, WHILE, LENGTH;

// Identifier
terminal String ID;

// Types
terminal String TYPE_INT, TYPE_BOOL;

// Values
terminal String INT_LITERAL; // n < 2^63
terminal INT_MAX; // n == 2^63
terminal String STRING_LITERAL;
terminal String CHAR_LITERAL;
terminal Boolean BOOL_LITERAL;

// Punctuation
terminal L_PAREN, R_PAREN, L_SQ_BRKT, R_SQ_BRKT, L_BRACE, R_BRACE;
terminal COLON, SEMICOLON, COMMA, UNDERSCORE;

// Operators
terminal ASSIGN,
         PLUS,
         MINUS,
         MULT,
         HIGH_MULT,
         DIVIDE,
         EQUALS,
         NOT_EQUALS,
         LT,
         LTE,
         GT,
         GTE,
         NEG_BOOL,
         NEG_INT,
         REMAINDER,
         LOGICAL_AND,
         LOGICAL_OR
         ;
         
// Compound-Assignment Operators
terminal COMPOUND_PLUS,
         COMPOUND_MINUS,
         COMPOUND_MULT,
         COMPOUND_HIGH_MULT,
         COMPOUND_DIV,
         COMPOUND_REM,
         COMPOUND_AND,
         COMPOUND_OR;

// Program-wide
nonterminal AbstractNode Program;

// Type Expressions

nonterminal TypeExprNode TypeExpr;
nonterminal PrimitiveTypeNode Primitive;
nonterminal LinkedList<Optional<ExprNode>> TypeExprArrayList;

// Ixi program

nonterminal IxiProgramNode IxiProgram;
nonterminal LinkedList<FunctionHeaderDeclNode> FunctionHeaderDeclList;
nonterminal FunctionHeaderDeclNode FunctionHeaderDecl;

nonterminal LinkedList<VarDeclNode> FunctionVarDeclList;

nonterminal LinkedList<TypeExprNode> ReturnType;
nonterminal LinkedList<TypeExprNode> ReturnTypeList;

// Xi program

nonterminal XiProgramNode XiProgram;
nonterminal LinkedList<UseNode> UseList;
nonterminal LinkedList<FunctionDeclNode> FunctionDeclList;
nonterminal FunctionDeclNode FunctionDecl;


// Xi Statements

nonterminal StmtNode XiStatement;
nonterminal LinkedList<StmtNode> XiStatementList;
nonterminal StmtNode SingleXiStatement;
nonterminal CompoundAssignStmtNode CompoundAssignmentStatement;
nonterminal ReturnStmtNode ReturnStatement;
nonterminal BlockStmtNode BlockStatement;
nonterminal ProcedureStmtNode ProcedureStatement;
nonterminal IfElseStmtNode IfElseStatement;
nonterminal WhileStmtNode WhileStatement;
nonterminal AssignmentStmtNode AssignmentStatement;
nonterminal ExprNode AssignmentLHS;

nonterminal StmtNode VarDeclStatement;
nonterminal VarDeclNode VarDecl;

nonterminal ArrayDeclStmtNode VarDeclArrayInit;
nonterminal LinkedList<Optional<ExprNode>> VarDeclDimensionedBracket;
nonterminal LinkedList<Optional<ExprNode>> VarDeclDimensionlessBracket;

nonterminal LinkedList<Optional<VarDeclNode>> MultiVarDeclList;
nonterminal LinkedList<ExprNode> ExprList;

nonterminal ExprNode Expr;
nonterminal ExprNode AccessExpr;
nonterminal FunctionCallExprNode FunctionCall;
nonterminal FunctionCallExprNode ProcedureCall;
nonterminal LinkedList<ExprNode> ArgParamList;
nonterminal ExprNode LiteralExpr;
nonterminal LinkedList<ExprNode> ArrayElements;
nonterminal ExprNode UnaryExpr;
nonterminal ExprNode BinaryExpr;

// Precedence

precedence nonassoc SEMICOLON;
precedence nonassoc L_BRACE;

precedence nonassoc IF;
precedence nonassoc ELSE;

precedence left LOGICAL_OR;
precedence left LOGICAL_AND;
precedence left EQUALS, NOT_EQUALS;
precedence left LT, LTE, GTE, GT;
precedence left PLUS, MINUS;
precedence left MULT, HIGH_MULT, DIVIDE, REMAINDER;
precedence nonassoc NEG_INT, NEG_BOOL;
precedence left L_PAREN, L_SQ_BRKT;

// Grammar

start with Program;

Program ::=
    IXI_FILE IxiProgram:ixi {: RESULT = ixi; :}
    | XI_FILE XiProgram:xi {: RESULT = xi; :}
    ;

// Ixi Grammar

IxiProgram ::=
    FunctionHeaderDeclList:lst {: RESULT = new IxiProgramNode(lstxleft, lst); :}
    ;

FunctionHeaderDeclList ::=
    FunctionHeaderDeclList:lst FunctionHeaderDecl:decl {:
            lst.addLast(decl);
            RESULT = lst;
        :}
    | FunctionHeaderDecl:decl {: 
            RESULT = new LinkedList<>(List.of(decl)); 
        :}
    ;

FunctionHeaderDecl ::=
    ID:id L_PAREN R_PAREN ReturnType:returnList {:
            RESULT = new FunctionHeaderDeclNode(idxleft, id, new LinkedList<>(), returnList);
        :}
    | ID:id L_PAREN VarDecl:v FunctionVarDeclList:l R_PAREN ReturnType:returnList {:
            l.addFirst(v);
            RESULT = new FunctionHeaderDeclNode(idxleft, id, l, returnList);
        :}
    ;

FunctionVarDeclList ::=
    COMMA VarDecl:v FunctionVarDeclList:l {:
            l.addFirst(v);
            RESULT = l;
        :}
    | {: RESULT = new LinkedList<>(); :}
    ;

ReturnType ::=
    COLON TypeExpr:t ReturnTypeList:lst {: lst.addFirst(t); RESULT = lst; :}
    | {: RESULT = new LinkedList<>(); :}
    ;

ReturnTypeList ::=
    ReturnTypeList:lst COMMA TypeExpr:t {: lst.addLast(t); RESULT = lst; :}
    | {: RESULT = new LinkedList<>(); :}
    ;

// Xi Grammar

XiProgram ::=
    UseList:uses FunctionDeclList:functions {: 
        RESULT = new XiProgramNode(usesxleft, uses, functions); 
    :}
    ;

UseList ::=
    USE:u ID:id UseList:l {: 
        l.addFirst(new UseNode(uxleft, id)); RESULT = l; 
    :}
    | USE:u ID:id SEMICOLON UseList:l {: 
        l.addFirst(new UseNode(uxleft, id)); RESULT = l; 
    :}
    | {: RESULT = new LinkedList<>(); :}
    ;

// Xi statements with no semicolon excluding return
XiStatement ::=
    BlockStatement:s {: RESULT = s; :}
    | ProcedureStatement:s {: RESULT = s; :}
    | IfElseStatement:s {: RESULT = s; :}
    | WhileStatement:s {: RESULT = s; :}
    | AssignmentStatement:s {: RESULT = s; :}
    | VarDeclStatement:s {: RESULT = s; :}
    | CompoundAssignmentStatement: s {: RESULT = s; :}
    ;

// Xi statement list excluding return
XiStatementList ::=
    SingleXiStatement:s XiStatementList:l {: l.addFirst(s); RESULT = l; :}
    | {: RESULT = new LinkedList<>(); :}
    ;

SingleXiStatement ::=
    XiStatement:s {: RESULT = s; :}
    | XiStatement:s SEMICOLON {: RESULT = s; :}
    ;

FunctionDeclList ::=
    FunctionDecl:fd FunctionDeclList:l {: l.addFirst(fd); RESULT = l; :}
    | FunctionDecl: fd {: 
        RESULT = new LinkedList<>(List.of(fd));
    :}
    ;

FunctionDecl ::=
    FunctionHeaderDecl:header BlockStatement:block {:
            RESULT = new FunctionDeclNode(headerxleft, header, block);
        :}
    ;

BlockStatement ::=
    L_BRACE:b XiStatementList:l R_BRACE {: 
        RESULT = new BlockStmtNode(bxleft, l); 
    :}
    | L_BRACE:b XiStatementList:l ReturnStatement:r R_BRACE {: 
        l.addLast(r); RESULT = new BlockStmtNode(bxleft, l); 
    :}
    | L_BRACE:b XiStatementList:l ReturnStatement:r SEMICOLON R_BRACE {: 
        l.addLast(r); RESULT = new BlockStmtNode(bxleft, l); 
    :}
    ;

ReturnStatement ::=
    RETURN:r ExprList:l {: RESULT = new ReturnStmtNode(rxleft, l); :}
    ;

ProcedureStatement ::=
    ProcedureCall:c {: RESULT = new ProcedureStmtNode(cxleft, c); :}
    ;

IfElseStatement ::=
    IF:i Expr:e SingleXiStatement:s {: 
        RESULT = new IfElseStmtNode(ixleft, e, s, Optional.empty()); 
    :}
    | IF:i Expr:e SingleXiStatement:s ELSE SingleXiStatement:b {:
            RESULT = new IfElseStmtNode(ixleft, e, s, Optional.of(b));
        :}
    ;

WhileStatement ::=
    WHILE:w Expr:e XiStatement:s {: 
        RESULT = new WhileStmtNode(wxleft, e, s); 
    :}
    ;

AssignmentStatement ::=
    AssignmentLHS:a ASSIGN Expr:e {: 
        RESULT = new AssignmentStmtNode(axleft, a, e); 
    :}
    ;

AssignmentLHS ::=
    AssignmentLHS:a L_SQ_BRKT Expr:e R_SQ_BRKT {:
        RESULT = new ArrayAccessExprNode(axleft, a, e);
        :}
    | FunctionCall:f L_SQ_BRKT Expr:e R_SQ_BRKT {:
        RESULT = new ArrayAccessExprNode(exleft, f, e);
    :}
    | ID:id {: 
        RESULT = new VariableAccessExprNode(idxleft, id);
    :}
    ;

CompoundAssignmentStatement ::= 
    AssignmentLHS:a COMPOUND_PLUS Expr:e {:
        RESULT = new CompoundAssignStmtNode(axleft, CompoundOpType.PLUS, a, e);
    :}
    | AssignmentLHS:a COMPOUND_MINUS Expr:e {:
        RESULT = new CompoundAssignStmtNode(axleft, CompoundOpType.MINUS, a, e);
    :}
    | AssignmentLHS:a COMPOUND_MULT Expr:e {:
        RESULT = new CompoundAssignStmtNode(axleft, CompoundOpType.MULT, a, e);
    :}
    | AssignmentLHS:a COMPOUND_HIGH_MULT Expr:e {:
        RESULT = new CompoundAssignStmtNode(axleft, CompoundOpType.HIGH_MULT, a, e);
    :}
    | AssignmentLHS:a COMPOUND_DIV Expr:e {:
        RESULT = new CompoundAssignStmtNode(axleft, CompoundOpType.DIV, a, e);
    :}
    | AssignmentLHS:a COMPOUND_REM Expr:e {:
        RESULT = new CompoundAssignStmtNode(axleft, CompoundOpType.REM, a, e);
    :}
    | AssignmentLHS:a COMPOUND_AND Expr:e {:
        RESULT = new CompoundAssignStmtNode(axleft, CompoundOpType.AND, a, e);
    :}
    | AssignmentLHS:a COMPOUND_OR Expr:e {:
        RESULT = new CompoundAssignStmtNode(axleft, CompoundOpType.OR, a, e);
    :}
    ;


VarDeclStatement ::=
    VarDeclArrayInit:arr {:
            RESULT = arr;
        :}
    | UNDERSCORE:u ASSIGN Expr:e {:
            RESULT = new ExprStmtNode(uxleft, e);
        :}
    | VarDecl:v ASSIGN Expr:e {:
            RESULT = new VarInitStmtNode(vxleft, v, e);
        :}
    | VarDecl:v {:
            RESULT = new VarDeclStmtNode(vxleft, v);
        :}
    | VarDecl:v COMMA MultiVarDeclList:l ASSIGN FunctionCall:e {:
            l.addFirst(Optional.of(v));
            RESULT = new MultiAssignStmtNode(vxleft, l, e);
        :}
    | UNDERSCORE:u COMMA MultiVarDeclList:l ASSIGN FunctionCall:e {:
            l.addFirst(Optional.empty());
            RESULT = new MultiAssignStmtNode(uxleft, l, e);
        :}
    ;

MultiVarDeclList ::=
    VarDecl:v COMMA MultiVarDeclList:l {:
            l.addFirst(Optional.of(v));
            RESULT = l;
        :}
    | UNDERSCORE COMMA MultiVarDeclList:l {:
            l.addFirst(Optional.empty());
            RESULT = l;
        :}
    | VarDecl:v  {:
            LinkedList<Optional<VarDeclNode>> l = new LinkedList<>();
            l.addFirst(Optional.of(v));
            RESULT = l;
        :}
    | UNDERSCORE {:
            LinkedList<Optional<VarDeclNode>> l = new LinkedList<>();
            l.addFirst(Optional.empty());
            RESULT = l;
        :}
    ;

VarDeclArrayInit ::=
    ID:id COLON Primitive:p L_SQ_BRKT Expr:v R_SQ_BRKT 
    VarDeclDimensionedBracket:l {:
            l.addFirst(Optional.of(v));
            TypeExprArrayNode t = TypeExprNode.fromDimensionList(p, l);
            RESULT = new ArrayDeclStmtNode(idxleft, id, t);
        :}
    ;

VarDeclDimensionedBracket ::=
    L_SQ_BRKT Expr:v R_SQ_BRKT VarDeclDimensionedBracket:l {:
            l.addFirst(Optional.of(v));
            RESULT = l;
        :}
    | VarDeclDimensionlessBracket:l {: RESULT = l; :}
    ;

VarDeclDimensionlessBracket ::= 
    L_SQ_BRKT R_SQ_BRKT VarDeclDimensionlessBracket:l {:
            l.addFirst(Optional.empty());
            RESULT = l;
        :}
    | {: RESULT = new LinkedList<>(); :}
    ;

VarDecl ::=
    ID:id COLON TypeExpr:te {: RESULT = new VarDeclNode(idxleft, id, te); :}
    ;

Primitive ::=
    TYPE_INT:i {: 
        RESULT = new PrimitiveTypeNode(ixleft, PrimitiveEnum.INT); 
    :}
    | TYPE_BOOL:b {: 
        RESULT = new PrimitiveTypeNode(bxleft, PrimitiveEnum.BOOL); 
    :}
    ;

TypeExpr ::=
    Primitive:p L_SQ_BRKT R_SQ_BRKT TypeExprArrayList:l {:
            l.addFirst(Optional.empty());
            RESULT = TypeExprNode.fromDimensionList(p, l);
        :}
    | Primitive:p {: RESULT = p; :}
    ;

TypeExprArrayList ::=
    L_SQ_BRKT R_SQ_BRKT TypeExprArrayList:l {: 
        l.addFirst(Optional.empty()); RESULT = l; 
    :}
    | {: RESULT = new LinkedList<>(); :} ;

ExprList ::=
    Expr:e COMMA ExprList:l {: l.addFirst(e); RESULT = l; :}
    | Expr:e {: RESULT = new LinkedList<>(List.of(e)); :}
    | {: RESULT = new LinkedList<>(); :}
    ;

Expr ::=
    AccessExpr:e {: RESULT = e; :}
    | FunctionCall:e {: RESULT = e; :}
    | LiteralExpr:e  {: RESULT = e; :}
    | UnaryExpr:e {: RESULT = e; :}
    | BinaryExpr:e {: RESULT = e; :}
    | L_PAREN Expr:e R_PAREN {: RESULT = e; :}
    ;

AccessExpr ::=
    ID:id {: RESULT = new VariableAccessExprNode(idxleft, id); :}
    | Expr:e L_SQ_BRKT:l Expr:i R_SQ_BRKT {:
            RESULT = new ArrayAccessExprNode(lxleft, e, i);
        :}
    ;
// can't have the length call in a procedure
ProcedureCall ::=
    ID:id L_PAREN R_PAREN {:
            RESULT = new FunctionCallExprNode(idxleft, id, new LinkedList<>());
        :}
    | ID:id L_PAREN Expr:e ArgParamList:l R_PAREN {:
            l.addFirst(e);
            RESULT = new FunctionCallExprNode(idxleft, id, l);
        :}
    ;

// For an explanation of %prec https://www.cs.princeton.edu/~appel/modern/java/CUP/manual.html#cpp
FunctionCall ::=
    ID:id L_PAREN R_PAREN {:
            RESULT = new FunctionCallExprNode(idxleft, id, new LinkedList<>());
        :}
    | ID:id L_PAREN Expr:e ArgParamList:l R_PAREN {:
            l.addFirst(e);
            RESULT = new FunctionCallExprNode(idxleft, id, l);
        :}
    ;

ArgParamList ::=
    COMMA Expr:e ArgParamList:l {: l.addFirst(e); RESULT = l; :}
    | {: RESULT = new LinkedList<>();:}
    ;

LiteralExpr ::=
    INT_LITERAL:l {: RESULT = new LiteralIntExprNode(lxleft, l); :}
    | CHAR_LITERAL:l {: RESULT = new LiteralCharExprNode(lxleft, l); :}
    | STRING_LITERAL:l {: RESULT = new LiteralStringExprNode(lxleft, l); :}
    | BOOL_LITERAL:l {: RESULT = new LiteralBoolExprNode(lxleft, l); :}
    | L_BRACE ArrayElements:l R_BRACE {: 
        RESULT = new LiteralArrayExprNode(lxleft, l); 
    :}
    ;

ArrayElements ::=
    Expr:e COMMA ArrayElements:l {: 
        l.addFirst(e); RESULT = l;
    :}
    | Expr:e {:
            LinkedList<ExprNode> l = new LinkedList<>();
            l.add(e);
            RESULT = l;
         :}
    | {: RESULT = new LinkedList<>(); :}
    ;

UnaryExpr ::=
    NEG_BOOL:n Expr:e {: RESULT = new BoolNegExprNode(nxleft, e); :}
    | MINUS:m Expr:e {:
            RESULT = new IntNegExprNode(mxleft, e);
        :} %prec NEG_INT
    | MINUS:m INT_MAX:i {:
            ExprNode e = new LiteralIntExprNode(ixleft, MyLexer.maxIntegerString);
            RESULT = new IntNegExprNode(mxleft, e);
        :} %prec NEG_INT
    | LENGTH:len L_PAREN Expr:e R_PAREN {:
            RESULT = new LengthExprNode(lenxleft, e);
        :}
    ;

BinaryExpr ::=
    Expr:e1 MULT Expr:e2 {: RESULT = new BinExprNode(e1xleft, ASTOpType.MUL, e1, e2); :}
    | Expr:e1 HIGH_MULT Expr:e2 {: RESULT = new BinExprNode(e1xleft, ASTOpType.HIGH_MUL, e1, e2); :}
    | Expr:e1 DIVIDE Expr:e2 {: RESULT = new BinExprNode(e1xleft, ASTOpType.DIV, e1, e2); :}
    | Expr:e1 REMAINDER Expr:e2 {: RESULT = new BinExprNode(e1xleft, ASTOpType.MOD, e1, e2); :}
    | Expr:e1 PLUS Expr:e2 {: RESULT = new BinExprNode(e1xleft, ASTOpType.ADD, e1, e2); :}
    | Expr:e1 MINUS Expr:e2 {: RESULT = new BinExprNode(e1xleft, ASTOpType.SUB, e1, e2); :}
    | Expr:e1 LT Expr:e2 {: RESULT = new BinExprNode(e1xleft, ASTOpType.LT, e1, e2); :}
    | Expr:e1 LTE Expr:e2 {: RESULT = new BinExprNode(e1xleft, ASTOpType.LEQ, e1, e2); :}
    | Expr:e1 GT Expr:e2 {: RESULT = new BinExprNode(e1xleft, ASTOpType.GT, e1, e2); :}
    | Expr:e1 GTE Expr:e2 {: RESULT = new BinExprNode(e1xleft, ASTOpType.GEQ, e1, e2); :}
    | Expr:e1 EQUALS Expr:e2 {: RESULT = new BinExprNode(e1xleft, ASTOpType.EQ, e1, e2); :}
    | Expr:e1 NOT_EQUALS Expr:e2 {: RESULT = new BinExprNode(e1xleft, ASTOpType.NEQ, e1, e2); :}
    | Expr:e1 LOGICAL_AND Expr:e2 {: RESULT = new BinExprNode(e1xleft, ASTOpType.AND, e1, e2); :}
    | Expr:e1 LOGICAL_OR Expr:e2 {: RESULT = new BinExprNode(e1xleft, ASTOpType.OR, e1, e2); :}
    ;

